plugins {
    id 'kotlin-multiplatform'
//    kotlin('multiplatform') version versions.kotlin
    id 'kotlinx-serialization'
    id 'com.android.library'
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
        }
    }
}

kotlin {
    android()
    iosX64('ios') {
        binaries {
            framework()
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation deps.kotlin.stdlib.common
                api deps.kotlin.coroutines.common
                implementation deps.kotlin.serialization.common

                implementation deps.ktor.core
                implementation deps.ktor.json
            }
        }
        commonTest {
            dependencies {
                implementation deps.kotlin.test.common
                implementation deps.kotlin.test.annotations
            }
        }
        androidMain {
            dependsOn commonMain
            dependencies {
                api deps.kotlin.stdlib.jdk
                api deps.kotlin.coroutines.jdk
                api deps.kotlin.coroutines.android
                implementation deps.kotlin.serialization.jdk

                implementation deps.ktor.android
                implementation deps.ktor.jsonJvm
            }
        }
        androidTest {
            dependsOn androidMain
            dependencies {
                implementation deps.kotlin.test.jdk
                implementation deps.kotlin.test.junit

                implementation deps.robolectric.core
            }
        }
        iosMain {
            dependsOn commonMain
            dependencies {
                implementation deps.kotlin.coroutines.native
                implementation deps.kotlin.serialization.native

                implementation deps.ktor.ios
                implementation deps.ktor.jsonIos
            }
        }
        iosTest {
        }
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

// This task attaches native framework built from ios module to Xcode project
// (see iosApp directory). Don't run this task directly,
// Xcode runs this task itself during its build process.
// Before opening the project from iosApp directory in Xcode,
// make sure all Gradle infrastructure exists (gradle.wrapper, gradlew).
task copyFramework {
    def buildType = project.findProperty("kotlin.build.type") ?: "DEBUG"
    def target = project.findProperty("kotlin.target") ?: "ios"
    def bin = kotlin.targets."$target".compilations.main.target.binaries.findFramework("", buildType)
    dependsOn bin.linkTask

    doLast {
        def srcFile = kotlin.targets."$target".compilations.main.target.binaries.findFramework("", buildType).outputFile
        def targetDir = getProperty("configuration.build.dir")
        copy {
            from srcFile.parent
            into targetDir
            include 'data.framework/**'
            include 'data.framework.dSYM'
        }
    }
}
